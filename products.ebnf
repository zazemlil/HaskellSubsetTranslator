#{ 5 
S -> DEFINITIONS

DEFINITIONS -> DEFINITION DEFINITIONS`
DEFINITIONS` -> DEFINITION DEFINITIONS`
        | ε

DEFINITION -> SIGNATURE
        | SUPERCOMBINATOR
        | DATA_TYPE_DECL

SIGNATURE -> id "::" TYPE_SIGNATURE
#} 5


#{ 6
TYPE_SIGNATURE -> TYPE TYPE_SIGNATURE`
TYPE_SIGNATURE` -> "->" TYPE TYPE_SIGNATURE`
        | ε

TYPE -> SIMPLE_TYPE
        | "(" TYPE_SIGNATURE ")"

SIMPLE_TYPE -> id
        | "Int"
        | "Float"
        | "String"
        | "Bool"
        | LIST_TYPE
        | "(" type_constructor TYPE_ARGUMENTS ")"

LIST_TYPE -> "[" TYPE "]"

TYPE_ARGUMENTS -> TYPE TYPE_ARGUMENTS
        | ε
#} 6


#{ 7
SUPERCOMBINATOR -> FUNCTION_DECL
        |  VARIABLE_DECL

FUNCTION_DECL -> id PATTERNS "=" EXPR
VARIABLE_DECL -> id "=" EXPR
#} 7


#{ 8
DATA_TYPE_DECL -> "data" CONSTRUCTOR "=" CONSTRUCTORS

CONSTRUCTORS -> CONSTRUCTOR CONSTRUCTORS`
CONSTRUCTORS` -> "|" CONSTRUCTOR CONSTRUCTORS`
        | ε

CONSTRUCTOR -> "(" type_constructor TYPE_ARGUMENTS ")"
#} 8


#{ 4
PATTERNS -> PATTERN PATTERNS`
PATTERNS` -> PATTERN PATTERNS`
        | ε

PATTERN -> id
        | LITERAL
        | "_"
        | "(" CONSTRUCTOR_PATTERN ")"
        | LIST_PATTERN

CONSTRUCTOR_PATTERN -> type_constructor PATTERNS
        | type_constructor

LIST_PATTERN -> "[" LIST_PATTERNS "]"
        | "(" id ":" id ")"
LIST_PATTERNS -> PATTERN LIST_PATTERNS`
        | ε
LIST_PATTERNS` -> "," PATTERN LIST_PATTERNS`
        | ε
#} 4


#{ 3
LIST_COMPREHENSION -> "[" EXPR "|" QUALIFIERS "]"
QUALIFIERS -> QUALIFIER QUALIFIERS`
QUALIFIERS` -> "," QUALIFIER QUALIFIERS`
        | ε
QUALIFIER -> id "<-" EXPR
        | "let" BIND
        | EXPR
#} 3


#{ 2
LET_EXPR -> "let" "{" BINDINGS "}" "in" EXPR
IF_EXPR -> "if" EXPR "then" EXPR "else" EXPR
LAMBDA_EXPR -> "\\" PATTERNS "->" EXPR

BINDINGS -> BIND ";" BINDINGS`
BINDINGS` -> BIND ";" BINDINGS`
        | ε
BIND -> id "=" EXPR 
        | id PATTERNS "=" EXPR 
#} 2


#{ 9
OR_EXPR -> OR_EXPR "||" AND_EXPR
        | AND_EXPR

AND_EXPR -> AND_EXPR "&&" COMP_EXPR
        | COMP_EXPR

COMP_EXPR -> COMP_EXPR "==" ADDITIVE_EXPR 
        | COMP_EXPR "/=" ADDITIVE_EXPR
        | COMP_EXPR "<" ADDITIVE_EXPR
        | COMP_EXPR ">" ADDITIVE_EXPR
        | COMP_EXPR "<=" ADDITIVE_EXPR
        | COMP_EXPR ">=" ADDITIVE_EXPR
        | ADDITIVE_EXPR

ADDITIVE_EXPR -> ADDITIVE_EXPR "+" MULTIPLICATIVE_EXPR
        | ADDITIVE_EXPR "-" MULTIPLICATIVE_EXPR
        | MULTIPLICATIVE_EXPR

MULTIPLICATIVE_EXPR -> MULTIPLICATIVE_EXPR "*" TERM
        | MULTIPLICATIVE_EXPR "/" TERM
        | TERM

TERM -> LITERAL
        | id
        | type_constructor
        | LIST_COMPREHENSION
        | "(" EXPR ")"
        | "[" LIST_ELEMENTS "]"
        | "(" FUNCTION_CALL ")"

LIST_ELEMENTS -> EXPR LIST_ELEMENTS`
        | ε
LIST_ELEMENTS` -> "," EXPR LIST_ELEMENTS`
        | ε

FUNCTION_CALL -> id TERM ARG_LIST
        | "head" TERM
        | "tail" TERM
ARG_LIST -> TERM ARG_LIST
        | ε
#} 9


#{ 1
EXPR -> LET_EXPR
        | IF_EXPR
        | LAMBDA_EXPR
        | LIST_COMPREHENSION
        | OR_EXPR


LITERAL -> literal_int
        | literal_float
        | literal_string
        | "True"
        | "False"

id -> [a-z][a-zA-z|0-9]*
type_constructor -> [A-Z][a-zA-z|0-9]*
literal_int -> [-]?[0-9]+
literal_float -> [-]?[0-9]+"."[0-9]+
literal_string -> \"[^"]*\"

"
#} 1

# нужен ли case of?